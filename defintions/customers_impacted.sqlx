config {
  type: "table",
  schema: "hail_demo",
  name: "customers_impacted",
  columns: {
    impact_probability: "Likelihood of hail impacting the customer",
    significant_severe: "Will the impact be significant/severe?",
    county_name: "Name and LSAD of the county where the customer is located",
    event_timestamp: "Timestamp of when the hail event occured",
    primary_key_id: "Concat of fdic_certificate_number and branch_name to uniquely identify each row",
    fdic_certificate_number: "A unique number assigned by the FDIC used to identify institutions and for the issuance of insurance certificates.",
    institution_name: "The legal name of the institution.",
    branch_name: "Name of the branch.",
    branch_number: "The branch's corresponding office number.",
    main_office: "Identifies whether or not this is the main office for the institution",
    branch_address: "Street address at which the branch is physically located.",
    branch_city: "City in which branch is physically located.",
    zip_code: "The first five digits of the full postal zip code representing physical location of the branch.",
    branch_county: "County where the branch is physically located.",
    county_fips_code: "A five digit number representing the state and county in which the institution is physically located. The first two digits represent the FIPS state numeric code and the last three digits represent the FIPS county numeric code.",
    state: "State abbreviation in which the branch is physically located. The FDIC Act defines state as any State of the United States, the District of Columbia, and any territory of the United States, Puerto Rico, Guam, American Samoa, the Trust Territory of the Pacific Islands, the Virgin Island, and the Northern Mariana Islands.",
    state_name: "Full-text name of the state in which the branch is physically located. The FDIC Act defines state as any State of the United States, the District of Columbia, and any territory of the United States, Puerto Rico, Guam, American Samoa, the Trust Territory of the Pacific Islands, the Virgin Island, and the Northern Mariana Islands.",
    institution_class: "A classification code assigned by the FDIC based on the institution's charter type (commercial bank or savings institution), charter agent (state or federal), Federal Reserve membership status (Fed member, Fed nonmember) and its primary federal regulator (state chartered institutions are subject to both federal and state supervision). N -Commercial bank, national (federal) charter and Fed member, supervised by the Office of the Comptroller of the Currency (OCC) NM -Commercial bank, state charter and Fed nonmember, supervised by the FDIC OI - Insured U.S. branch of a foreign chartered institution (IBA) SA - Savings associations, state or federal charter, supervised by the Office of Thrift Supervision (OTS) SB - Savings banks, state charter, supervised by the FDIC SM - Commercial bank, state charter and Fed member, supervised by the Federal Reserve (FRB)",
    cbsa_fips_code: "Numeric code of the Core Based Statistical Area (CBSA) as defined by the US Census Bureau Office of Management and Budget.",
    cbsa_name: "Name of the Core Based Statistical Area (CBSA) as defined by the US Census Bureau Office of Management and Budget.",
    cbsa_division_flag: "A flag indicating member of a Core Based Statistical Division as defined by the US Census Bureau Office of Management and Budget.",
    cbsa_division_flag_code: "Numeric code of the Core Based Statistical Division as defined by the US Census Bureau Office of Management and Budget.",
    cbsa_division_name: "Name of the Core Based Statistical Division as defined by the US Census Bureau Office of Management and Budget.",
    cbsa_metro_flag: "A flag used to indicate whether an branch is in a Metropolitan Statistical Area as defined by the US Census Bureau Office of Management and Budget",
    cbsa_metro_fips_code: "Numeric code of the Metropolitan Statistical Area as defined by the US Census Bureau Office of Management and Budget",
    cbsa_metro_name: "Name of the Metropolitan Statistical Area as defined by the US Census Bureau Office of Management and Budget",
    cbsa_micro_flag: "A flag used to indicate whether an branch is in a Micropolitan Statistical Area as defined by the US Census Bureau Office of Management and Budget",
    csa_flag: "Flag indicating member of a Combined Statistical Area (CSA) as defined by the US Census Bureau Office of Management and Budget	",
    csa_fips_code: "Numeric code of the Combined Statistical Area (CSA) as defined by the US Census Bureau Office of Management and Budget	",
    csa_name: "	Name of the Combined Statistical Area (CSA) as defined by the US Census Bureau Office of Management and Budget",
    date_established: "The date on which the branch began operations.",
    fdic_uninum: "	This is the FDIC UNINUM of the institution that owns the branch. A UNINUM is a unique sequentially number added to the FDIC database for both banks and branches. There is no pattern imbedded within the number. The FI_UNINUM is updated with every merger or purchase of branches to reflect the most current owner.",
    last_updated: "	The day the institution information was updated.",
    service_type: "Define the various types of offices of FDIC-insured institutions. 11 - Full Service Brick and Mortar Office 12 - Full Service Retail Office 13 - Full Service Cyber Office 14 - Full Service Mobile Office 15 - Full Service Home/Phone Banking 16 - Full Service Seasonal Office 21 - Limited Service Administrative Office 22 - Limited Service Military Facility 23 - Limited Service Facility Office 24 - Limited Service Loan Production Office 25 - Limited Service Consumer Credit Office 26 - Limited Service Contractual Office 27 - Limited Service Messenger Office 28 - Limited Service Retail Office 29 - Limited Service Mobile Office 30 - Limited Service Trust Office",
    branch_fdic_uninum: "Unique Identification Number for a Branch Office as assigned by the FDIC",
    customer_geom: "Geographic representation of the bank's physical location",
    customer_address: "Full address of the customer formatted for Looker Studio",
    customer_location: "Lat/Lon of the customer formatted for Looker Studio",
    state_fips_code: "FIPS code of the state where the customer is located"
  }
}

/* Determine which counties had any overlap with the storm area. This returns a list of counties and a few other key inputs from the hail event data.
This allows us to use counties as a way to subset our customers so we don't have to query every customer for every event.
The granularity on this search can vary by changing the geography. We use states for a broad subset of customers or census tracts for a more precise subset */
with hold AS (
  SELECT
    hail_polygons.LABEL AS LABEL,
    hail_polygons.LABEL2 AS LABEL2,
    PARSE_TIMESTAMP("%Y%m%d%H%M", hail_polygons.VALID, "-5:00") AS last_updated,
    hail_polygons.geometry AS hail_geom,
    counties.lsad_name,
    counties.county_geom,
    counties.county_fips_code,
    counties.state_fips_code
  FROM
    `hail_demo.county_boundaries` counties,
    `hail_demo.hail_events` hail_polygons
  WHERE
    counties.state_fips_code IN ("17", "18", "21", "26", "39", "54")
    AND ST_INTERSECTS(counties.county_geom, hail_polygons.geometry)
),

/* Find a list of all customers whose location falls within the counties that have overlap with the storm area.
This returns a list of all customers from within the counties that had overlap with the storm area */
potential_customers_impacted AS (
  SELECT
    customers.*
  FROM
    ${ref("convert_customer_geog")} customers,
    hold
  WHERE
    customers.state IN ("IL", "IN", "KY", "MI", "OH", "WV" )
    AND ST_WITHIN(customers.customer_geom, hold.county_geom)
)

/* This takes the customer subset identified in the previous statement, and determines if the customer location is within with the storm area.
This is more computationally efficient than checking if every customer nationwide falls within the storm area, even though it is an extra step */
SELECT
  CASE
    WHEN hold.LABEL = "0.05" THEN CAST(hold.LABEL as FLOAT64)
    WHEN hold.LABEL = "0.15" THEN CAST(hold.LABEL as FLOAT64)
    WHEN hold.LABEL = "SIGN" THEN CAST(REGEXP_EXTRACT(hold.LABEL2, r"^([\d]{2}).*") AS FLOAT64)/100
  END AS impact_probability,
  CASE
    WHEN hold.LABEL = "SIGN" THEN TRUE
    ELSE FALSE
  END as significant_severe,
  hold.lsad_name AS county_name,
  hold.last_updated AS valid,
  potential_customers_impacted.* EXCEPT (branch_address),
  CONCAT(branch_address, ", ", branch_city, ", ", state, " ", zip_code) AS customer_address,
  CONCAT(ST_Y(customer_geom), ", ", ST_X(customer_geom)) AS customer_location,
  hold.state_fips_code AS state_fips_code,
FROM
  potential_customers_impacted,
  hold
WHERE
  hold.state_fips_code IN ("17", "18", "21", "26", "39", "54")
  AND ST_WITHIN(potential_customers_impacted.customer_geom, hold.hail_geom)
